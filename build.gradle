plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.hellozai.weather'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(24)
	}
}

repositories {
	mavenCentral()
}

ext {
	set('springCloudVersion', "2025.0.0")
}
// Mockito jdk 21 compliance
configurations {
	mockitoAgent
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'

	// Cache deps
	implementation 'org.springframework.boot:spring-boot-starter-cache'
	implementation 'com.github.ben-manes.caffeine:caffeine'
//	implementation 'org.springframework.cloud:spring-cloud-starter-consul-discovery'

	// Documented as implicit but fallback doesn't work without it
	implementation 'org.springframework.boot:spring-boot-starter-aop'
	implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-resilience4j'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.mockito:mockito-core:5.18.0'
	mockitoAgent('org.mockito:mockito-core:5.18.0') {
		transitive = false
	}
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

tasks.named('test') {
	filter {
		excludeTestsMatching "*integration*"
	}
	useJUnitPlatform()
	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

def integrationTest = tasks.register("integrationTest", Test) {
	filter {
		includeTestsMatching '*.integration.*'
		includeTestsMatching '*Integration*'
	}
	useJUnitPlatform {
		group = 'verification'
	}
	testClassesDirs = sourceSets.test.output.classesDirs
	classpath = sourceSets.test.runtimeClasspath

	jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
}

tasks.named('check') {
	dependsOn integrationTest
}

bootJar {
	archiveFileName='hellozai-weather.jar'
}